// ASM: a very small and fast Java bytecode manipulation framework
// Copyright (c) 2000-2011 INRIA, France Telecom
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions
// are met:
// 1. Redistributions of source code must retain the above copyright
//    notice, this list of conditions and the following disclaimer.
// 2. Redistributions in binary form must reproduce the above copyright
//    notice, this list of conditions and the following disclaimer in the
//    documentation and/or other materials provided with the distribution.
// 3. Neither the name of the copyright holders nor the names of its
//    contributors may be used to endorse or promote products derived from
//    this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
// AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
// ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
// LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
// CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
// SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
// INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
// CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
// ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF
// THE POSSIBILITY OF SUCH DAMAGE.

repositories { mavenCentral() }

apply plugin: 'java'

description = 'ASM deprecated classes'
group = 'org.ow2.asm'
version = '7.1'

dependencies {
  implementation 'org.ow2.asm:asm:7.0'
  implementation 'org.ow2.asm:asm-commons:7.0'
}

compileJava.doLast {
  def retrofitter = new org.objectweb.asm.tools.Retrofitter()
  retrofitter.retrofit sourceSets.main.output.classesDirs.singleFile
}

jar {
  manifest {
    attributes(
      "Implementation-Title": project.description,
      "Implementation-Version": project.version)
  }
}

// Add tasks to generate the Javadoc and a source jar, to be uploaded to Maven
// together with the main jar (containing the compiled code).
task javadocJar(type: Jar, dependsOn: 'javadoc') {
  from javadoc.destinationDir
  classifier 'javadoc'
}
task sourcesJar(type: Jar, dependsOn: 'classes') {
  from sourceSets.main.allSource
  classifier 'sources'
}
artifacts {
  archives javadocJar
  archives sourcesJar
}

// Add an uploadArchives task, provided by the maven plugin, to generate the
// project POM and to upload it, with the classes, sources and Javadoc jars.
apply plugin: 'maven'
uploadArchives.repositories.mavenDeployer {
  def nexusUserName = System.env.NEXUS_USER_NAME
  def nexusPassword = System.env.NEXUS_PASSWORD
  if (nexusUserName != null) {
    def baseUrl = 'http://repository.ow2.org/nexus/'
    repository(url: baseUrl + 'service/local/staging/deploy/maven2') {
      authentication(userName: nexusUserName, password: nexusPassword)
    }
    snapshotRepository(url: baseUrl + 'content/repositories/snapshots') {
      authentication(userName: nexusUserName, password: nexusPassword)
    }
  } else {
    repository url: 'file://localhost/tmp/myRepo/'
  }
  pom.project {
    parent {
      artifactId 'ow2'
      groupId 'org.ow2'
      version '1.5'
    }

    name jar.baseName
    description project.description
    packaging 'jar'
    inceptionYear '2000'
    licenses {
      license {
        name 'BSD'
        url 'http://asm.ow2.org/license.html'
      }
    }

    url 'http://asm.ow2.org/'
    mailingLists {
      mailingList {
        name 'ASM Users List'
        subscribe 'https://mail.ow2.org/wws/subscribe/asm'
        post 'asm@objectweb.org'
        archive 'https://mail.ow2.org/wws/arc/asm/'
      }
    }
  }
}

